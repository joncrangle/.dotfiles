# NOTE:
#   (`-')   (`-').-> (`-').->
#   ( OO).->( OO)_   (OO )__
# ,(_/----.(_)--\_) ,--. ,'-'
# |__,    |/    _ / |  | |  |
#  (_/   / \_..`--. |  `-'  |
#  .'  .'_ .-._)   \|  .-.  |
# |       |\       /|  | |  |
# `-------' `-----' `--' `--'
# ##############################
# #      ZSH CONFIGURATION     #
# ##############################

HISTORY_IGNORE="(ls|cd|pwd|zsh|exit|cd ..)"
HISTSIZE=10000
SAVEHIST=10000
LISTMAX=1000
COREPACK_ENABLE_AUTO_PIN=0

[[ -f "$HOME/.local/bin/mise" ]] && eval "$($HOME/.local/bin/mise activate zsh)"
export EDITOR="$(which nvim 2>/dev/null || which vim 2>/dev/null || echo /usr/bin/vi)"
export VISUAL="$EDITOR"
export SUDO_EDITOR="$EDITOR"

if [[ -f "$HOME/.config/mise/gh_public.json" ]]; then
  export MISE_GITHUB_TOKEN=$(jq -r '.github_token' "$HOME/.config/mise/gh_public.json")
fi

{{ if eq .chezmoi.os "darwin" -}}
[[ -f /opt/homebrew/bin/brew ]] && eval "$(/opt/homebrew/bin/brew shellenv)"
{{- end -}}
{{ if eq .chezmoi.os "linux" -}}
source "/usr/share/zsh-antidote/antidote.zsh"
{{- else }}
source "$HOMEBREW_DIR/opt/antidote/share/antidote/antidote.zsh"
{{- end }}
antidote load

# Zsh completion setup
typeset -U fpath
{{ if eq .chezmoi.os "linux" -}}
fpath=($ZDOTDIR/completions /usr/share/zsh/site-functions $fpath)
{{- else -}}
fpath=($ZDOTDIR/completions $HOMEBREW_DIR/share/zsh/site-functions $fpath)
{{- end }}
export ZSH_COMPDUMP="$ZDOTDIR/.zcompdump"
autoload -Uz compinit
compinit -d "$ZSH_COMPDUMP"

# Force autoload of custom completions
for f in $ZDOTDIR/completions/_(#qN); do
  autoload -Uz ${f:t}
done

function zvm_config() {
  ZVM_VI_SURROUND_BINDKEY=s-prefix
  ZVM_VI_HIGHLIGHT_FOREGROUND=#cdd6f4
  ZVM_VI_HIGHLIGHT_BACKGROUND=#45475a
  ZVM_VI_HIGHLIGHT_EXTRASTYLE=bold
  ZVM_INIT_MODE=sourcing
}

source "$ZDOTDIR/options.zsh"
source "$ZDOTDIR/aliases.zsh"
source "$ZDOTDIR/banner.zsh"

autoload -Uz add-zle-hook-widget

eval "$(starship init zsh)"

set-long-prompt() {
  PROMPT="$(starship prompt)"
  RPROMPT=""
}

set-short-prompt() {
  PROMPT="$(starship module character)"
  RPROMPT=$'%{\e[999C\e[8D%}%F{8}%*%f '
}

precmd_functions+=(set-long-prompt)

function transient-line-finish() {
  set-short-prompt
  zle .reset-prompt 2>/dev/null
}
zle -N transient-line-finish

# Trap: handle Ctrl+C
function transient-int() {
  set-short-prompt
  zle .reset-prompt 2>/dev/null
  return 130
}

function zvm_after_select_vi_mode() {
  case $ZVM_MODE in
  $ZVM_MODE_NORMAL)
    STARSHIP_KEYMAP=vicmd
    echo -ne "\e[1 q" # Block cursor
    ;;
  $ZVM_MODE_INSERT)
    STARSHIP_KEYMAP=viins
    echo -ne "\e[5 q" # Beam cursor
    ;;
  $ZVM_MODE_VISUAL | $ZVM_MODE_VISUAL_LINE)
    STARSHIP_KEYMAP=vicmd
    echo -ne "\e[1 q" # Block cursor
    ;;
  esac
  # Force Starship to redraw right away
  zle .reset-prompt
}

# ZVM Hooks
zvm_after_init_commands+=(
  # Bind the transient finish widget safely
  'zle -N zle-line-finish transient-line-finish'

  # Fix line init cursors
  'function zle-line-init() {
    zle -K viins
    echo -ne "\e[5 q"
  }
  zle -N zle-line-init'

  # sk source
  '[[ -f $HOME/.config/zsh/lib/sk_init.zsh ]] && source $HOME/.config/zsh/lib/sk_init.zsh'
)

# Apply the Ctrl+C trap
trap 'transient-int' INT
zstyle ':completion:*' group-name ''
zstyle ':fzf-tab:*' single-group header
zstyle ':completion:*' format '-- %d --'
zstyle ':completion:*:descriptions' format '-- %d --'
zstyle ':completion:*:messages' format '-- %d --'
zstyle ':completion:*:warnings' format '-- %d --'
zstyle ':fzf-tab:*' format '-- %d --'

bindkey -M vicmd ":" undefined-key
# vim: ft=zsh
